plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'de.mannodermaus.android-junit5'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.rey.movies"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "0.1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets.each { variant ->
        def variantName = variant.name

        variant.java.srcDir("src/$variantName/kotlin")

        if (variantName.toLowerCase().contains("test")) {
            variant.java.srcDirs += "src/testCommon/kotlin"
            variant.resources.srcDirs += "src/testCommon/resources"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    testOptions {
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }
        }

        unitTests.all {
            testLogging.events = ["passed", "skipped", "failed"]
        }
    }
    dynamicFeatures = [':list']
}

dependencies {
    ext {
        spek = "2.0.15"
        retrofit = "2.9.0"
        lifecycle = "2.3.1"
         navigation = "2.3.5"
    }

    api 'com.google.android.material:material:1.3.0'
    api 'com.github.reyvababtista:clean-arch-lib:0.1.0'
    api 'com.google.code.gson:gson:2.8.7'
    api "com.squareup.retrofit2:retrofit:$retrofit"
    api "com.squareup.retrofit2:converter-gson:$retrofit"
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2'
    api "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle"
    api "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle"
    api 'androidx.appcompat:appcompat:1.3.0'
    api 'androidx.constraintlayout:constraintlayout:2.0.4'
    api "androidx.navigation:navigation-fragment-ktx:$navigation"
    api "androidx.navigation:navigation-ui-ktx:$navigation"
    api "androidx.navigation:navigation-dynamic-features-fragment:$navigation"
    api 'com.github.bumptech.glide:glide:4.12.0'
    api 'com.google.dagger:dagger:2.35.1'
    kapt 'com.google.dagger:dagger-compiler:2.35.1'

    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek"
    testImplementation "org.spekframework.spek2:spek-runner-junit5:$spek"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.0'
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.1'

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}