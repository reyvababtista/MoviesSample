plugins {
    id 'com.android.dynamic-feature'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'de.mannodermaus.android-junit5'
}
android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.rey.list"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets.each { variant ->
        def variantName = variant.name

        variant.java.srcDir("src/$variantName/kotlin")

        if (variantName.toLowerCase().contains("test")) {
            variant.java.srcDirs += "src/testCommon/kotlin"
            variant.resources.srcDirs += "src/testCommon/resources"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }

    testOptions {
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }
        }

        unitTests.all {
            testLogging.events = ["passed", "skipped", "failed"]
        }
    }
}

dependencies {
    ext {
        spek = "2.0.15"
    }

    implementation project(":app")
    kapt 'com.google.dagger:dagger-compiler:2.35.1'

    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek"
    testImplementation "org.spekframework.spek2:spek-runner-junit5:$spek"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.0'
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.1'

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}